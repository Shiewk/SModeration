plugins {
    id 'java'
    id("xyz.jpenilla.run-paper") version "2.3.1"
}

group = 'de.shiewk'
version = pluginVersion

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('paper-plugin.yml') {
        expand props
    }
}

runPaper {
    folia {
        registerTask()
    }
}

runServer {
    minecraftVersion("1.21.10")
    downloadPlugins {
        // for testing from other client versions
        modrinth("ViaVersion", "5.5.1")
        modrinth("ViaBackwards", "5.5.1")
    }
}

dependencies {
    //compileOnly "io.papermc.paper:paper-api:1.21.4-R0.1-SNAPSHOT"
    compileOnly 'dev.folia:folia-api:1.21.4-R0.1-SNAPSHOT'
}

jar {
    archiveBaseName.set('SModeration-Paper')
    archiveVersion.set(pluginVersion)
}


def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}